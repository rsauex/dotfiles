(define-module (rsauex packages mikrotik)
  #:use-module ((gnu packages bash)         #:prefix bash:)
  #:use-module ((gnu packages wine)         #:prefix wine:)
  #:use-module ((guix build-system trivial) #:prefix trivial-build-system:)
  #:use-module ((guix download)             #:prefix download:)
  #:use-module ((guix packages))
  #:use-module ((nonguix licenses)          #:prefix non-license:))

(define-public winbox
  (package
    (name "winbox")
    (version "3.37")
    (source (origin (method download:url-fetch)
                    (uri (string-append "https://download.mikrotik.com/winbox/" version "/winbox.exe"))
                    (sha256 (base32 "1zla30bc755x5gfv9ff1bgjvpsjmg2d7jsjxnwwy679fry4n4cwl"))))
    (build-system trivial-build-system:trivial-build-system)
    (arguments (list #:modules '((guix build utils))
                     #:builder `(begin
                                  (use-modules (guix build utils))
                                  (let* ((source (assoc-ref %build-inputs "source"))
                                         (output (assoc-ref %outputs "out"))
                                         (winbox-output (string-append output "/share/winbox")))
                                    (mkdir-p output)
                                    (mkdir-p winbox-output)
                                    (copy-file source (string-append winbox-output "/winbox.exe"))
                                    (let* ((winbox (string-append winbox-output "/winbox"))
                                           (bin (string-append output "/bin"))
                                           (wrapper (string-append bin "/winbox"))
                                           (bash (assoc-ref %build-inputs "bash"))
                                           (wine (assoc-ref %build-inputs "wine")))
                                      (mkdir-p bin)
                                      (with-output-to-file wrapper
                                        (lambda _
                                          (format #t "#!~a/bin/bash~%" bash)
                                          (format #t "export WINEPREFIX=\"${WINBOX_HOME:-\"${XDG_DATA_HOME:-\"${HOME}/.local/share\"}/winbox\"}/wine\"~%")
                                          (format #t "export WINEARCH=\"win32\"~%")
                                          (format #t "export WINEDLLOVERRIDES=\"mscoree=\" # disable mono~%")
                                          (format #t "export WINEDEBUG=-all~%")
                                          (format #t "if [ ! -d \"$WINEPREFIX\" ] ; then~%")
                                          (format #t "  mkdir -p \"$WINEPREFIX\"~%")
                                          (format #t "  ~a/bin/wineboot -u~%" wine)
                                          (format #t "fi~%")
                                          (format #t "exec ~a/bin/wine ~a/winbox.exe \"$@\"~%" wine winbox-output)))
                                      (chmod wrapper #o755))))))
    (inputs `(,wine:wine
              ,bash:bash))
    (synopsis "GUI administration for Mikrotik RouterOS")
    (description "Graphical configuration utility for RouterOS-based devices")
    (home-page "https://mikrotik.com")
    (supported-systems (list "x86_64-linux"))
    (license (non-license:nonfree "https://mikrotik.com/downloadterms.html"))))
